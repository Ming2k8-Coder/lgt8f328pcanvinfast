const unsigned int SIF_PIN = 3;

short rpm = 0;
bool regen = false;
bool brake = false;
byte driveModeBits = 0;
String driveModeName = "";

unsigned long lastTime;
unsigned long lastDuration = 0;
byte lastCrc = 0;
byte data[12];
int bitIndex = -1;

void setup()
{
  Serial.begin(115200);
  Serial.println("Begin.");

  pinMode(SIF_PIN, INPUT);
  lastTime = micros();
  attachInterrupt(digitalPinToInterrupt(SIF_PIN), sifChange, CHANGE);
}

void loop()
{
  // ISR xử lý, loop không cần gì
}

void sifChange()
{
  int val = digitalRead(SIF_PIN);
  unsigned long duration = micros() - lastTime;
  lastTime = micros();

  if (val == LOW)
  {
    if (lastDuration > 0)
    {
      bool bitComplete = false;
      float ratio = float(lastDuration) / float(duration);

      // Sync frame
      if (round(lastDuration / duration) >= 31)
      {
        bitIndex = 0;
      }
      else if (ratio > 1.5)
      {
        // bit 0
        bitClear(data[bitIndex / 8], 7 - (bitIndex % 8));
        bitComplete = true;
      }
      else if ((1 / ratio) > 1.5)
      {
        // bit 1
        bitSet(data[bitIndex / 8], 7 - (bitIndex % 8));
        bitComplete = true;
      }

      if (bitComplete)
      {
        bitIndex++;
        if (bitIndex == 96) // đủ 12 byte
        {
          bitIndex = 0;
          byte crc = 0;

          for (int i = 0; i < 11; i++)
          {
            crc ^= data[i];
          }

          if (crc == data[11] && crc != lastCrc)
          {
            lastCrc = crc;

            // In raw data để debug
            for (int i = 0; i < 12; i++)
            {
              Serial.print(data[i], HEX);
              Serial.print(" ");
            }
            Serial.println();

            // Giải mã từ byte 4
            byte statusByte = data[4];
            brake = bitRead(statusByte, 5);  // bit 5 = brake
            regen = bitRead(statusByte, 3);  // bit 3 = regen
            bool reverse = bitRead(data[5], 2); // Bit 2 của byte 5


            // 3 bit thấp = chế độ lái
            driveModeBits = statusByte & 0b00000111;
            switch (driveModeBits) {
              case 0: driveModeName = "THE THAO"; break;
              case 1: driveModeName = "NORMAL"; break;
              case 2: driveModeName = "TIET KIEM"; break;
              default: driveModeName = "UNKNOWN"; break;
            }

            // RPM từ byte 7-8
            rpm = ((data[7] << 8) + data[8]);

            // In kết quả
            Serial.print("RPM: "); Serial.print(rpm);
            Serial.print("  Mode: "); Serial.print(driveModeName);
            if (brake) Serial.print("  BRAKE");
            if (regen) Serial.print("  REGEN");
            if (reverse) Serial.print("  SO LUI");
            Serial.println();
          }
        }
      }
    }
  }
  lastDuration = duration;
}
